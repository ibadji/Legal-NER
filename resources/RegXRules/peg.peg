grammar Canopy.PEG

grammar                   <-  __* grammar_name
                              rules:(__* grammar_rule)+ __*

grammar_name              <-  `grammar` ":"? __+ object_identifier

grammar_rule              <-  identifier assignment parsing_expression

assignment                <-  __+ "<-" __+

parsing_expression        <-  choice_expression / choice_part

parenthesised_expression  <-  "(" __* parsing_expression __* ")"

choice_expression         <-  first_part:choice_part
                              rest:(__+ "/" __+ expression:choice_part)+

choice_part               <-  (action_expression / sequence_expression / sequence_part)
                              (__+ type_tag)?

action_expression         <-  actionable_expression __+ action_tag

actionable_expression     <-  "(" __* actionable_expression __* ")"
                           /  sequence_expression
                           /  repeated_atom
                           /  terminal_node

action_tag                <-  "%" identifier

type_tag                  <-  "<" object_identifier ">"

sequence_expression       <-  first_part:sequence_part
                              rest:(__+ expression:sequence_part)+

sequence_part             <-  label? expression:(maybe_atom / repeated_atom / atom)

maybe_atom                <-  atom "?"

repeated_atom             <-  atom quantifier:("*" / "+")

atom                      <-  parenthesised_expression
                           /  predicated_atom
                           /  reference_expression
                           /  terminal_node

terminal_node             <-  string_expression
                           /  ci_string_expression
                           /  char_class_expression
                           /  any_char_expression

predicated_atom           <-  predicate:("&" / "!") atom

reference_expression      <-  identifier !assignment

string_expression         <-  '"' ("\\" . / [^"])* '"' / "'" ("\\" . / [^'])* "'"

ci_string_expression      <-  "`" ("\\" . / [^`])* "`"

any_char_expression       <-  "."

char_class_expression     <-  "[" "^"? ("\\" . / [^\]])+ "]"

label                     <-  identifier ":"

object_identifier         <-  identifier ("." identifier)*

identifier                <-  [a-zA-Z_] [a-zA-Z0-9_]*

__                        <-  [\s] / comment

comment <- "#" [^\n]*